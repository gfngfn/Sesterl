require "./gen_server.sest"

module SampleProject = struct

  module Server = struct

    type request_impl =
      | GetNumber
      | GetName

    type response_impl =
      | Number(int)
      | Name(binary)

    type cast_message_impl =
      | SetNumber(int)

    module Callback = struct

      type request = request_impl
      type response = response_impl
      type cast_message = cast_message_impl
      type state = { number : int,  name : binary }
      type init_arg = state

      let init(state) =
        let _ = print_debug(("init", state)) in
        state

      let handle_call(req, pid, state) =
        case req of
        | GetNumber -> (Number(state.number), state)
        | GetName   -> (Name(state.name), state)
        end

     let handle_cast(msg, state) =
       let _ = print_debug(("handle_cast", msg)) in
       case msg of
       | SetNumber(m) -> { number = m, name = state.name }
       end

    end

    include SesterlStdlib.GenServer.Make(Callback)

    let set_number<$a>(pid : proc, m : int) : [$a]unit =
      cast(pid, SetNumber(m))

    let get_number<$a>(pid : proc, ?timeout t_opt) : [$a]int =
      do res <-
        case t_opt of
        | None    -> call(pid, GetNumber)
        | Some(t) -> call(pid, GetNumber, ?timeout t)
        end
      in
      case res of
      | Number(n) -> return(n)
      end

  end

  module Main = struct

    letrec loop(t, pid) =
      if t <= 0 then
        return(())
      else
        do n <- Server.get_number(pid) in
        let _ = print_debug((t, n)) in
        loop(t - 1, pid)

    let main() =
      do pid <- Server.start_link({ number = 57, name = "Sample Store"}) in
      do x <- Server.get_number(pid, ?timeout 1000) in
      let _ = print_debug(("first get", x)) in
      do Server.set_number(pid, 42) in
      loop(10, pid)

  end

end
